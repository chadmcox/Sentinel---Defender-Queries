//currently set to query 7 days
SigninLogs
| where TimeGenerated > ago(7d) and ResultType == 0
| where AADTenantId == ResourceTenantId
| extend TrustedLocation = tostring(iff(NetworkLocationDetails contains 'trustedNamedLocation', 'trustedNamedLocation','NotTrusted'))
| extend networkName = tostring(parse_json(parse_json(NetworkLocationDetails)[0].networkNames)[0])
| extend trustType = tostring(DeviceDetail.trustType)
| extend isCompliant = tostring(DeviceDetail.isCompliant)
| summarize
    ['Total Signin Count']=count(),
    ['Total MFA Count']=countif(AuthenticationRequirement == "multiFactorAuthentication"),
    ['Total non MFA Count']=countif(AuthenticationRequirement == "singleFactorAuthentication"),
    ['Total Trusted device']=countif(trustType == "Hybrid Azure AD joined"),
    ['Total Compliant device']=countif(isCompliant == 'true'),
    ['Total Trusted Location']=countif(TrustedLocation == 'trustedNamedLocation'),
    ['Total CA Applied']=countif(ConditionalAccessStatus == 'success'),
    ['Number of Users']=dcount(UserPrincipalName) by TrustedLocation, networkName, Location
